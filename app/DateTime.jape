/*
*  DateTime.jape
*
* This file define new rules to GATE to annotate Slovak format dates and languages
*
*  Filip Ogurcak, 23 Okt 2012
* 
*/


Phase:	DateTime
Input: Token Lookup
Options: control = appelt



Macro: DAY_NAME 
({Lookup.minorType == day })

Macro: ONE_DIGIT
({Token.kind == number, Token.length == "1"})

Macro: TWO_DIGIT
({Token.kind == number, Token.length == "2"})

Macro: FOUR_DIGIT
({Token.kind == number, Token.length == "4"})

Macro: DAY_MONTH_NUM
(ONE_DIGIT | TWO_DIGIT)

Macro: DATE_PRE
// possible modifiers of dates, eg. "early October"
({Token.string == "early"}|
 {Token.string == "late"}|
 {Token.string == "mid"}|
 {Token.string == "mid-"}|
 {Token.string == "end"}
)

Macro: DAY 
(((DATE_PRE)?
  DAY_NAME) |
 DAY_MONTH_NUM)

Macro: MONTH_NAME
( (DATE_PRE)?
  {Lookup.minorType == month})

Macro: MONTH 
(MONTH_NAME | DAY_MONTH_NUM)

Macro: SLASH
  ({Token.string == "/"})
  
Macro: DOT
  ({Token.string == "."})
  
Macro: DASH
  {Token.string == "-"}

Macro: OF
  {Token.string == "of"}
  
Macro: SPACE
  {Token.string == " "}
  

Macro: AD_BC
	(  {Token.string == "ad"} | {Token.string == "AD"}
	|
	  ({Token.string == "a"} {Token.string == "."}
	   {Token.string == "d"} {Token.string == "."})
	|
	  ({Token.string == "A"} {Token.string == "."}
	   {Token.string == "D"} {Token.string == "."})
	|

	  {Token.string == "bc"} | {Token.string == "BC"}
	|
	  ({Token.string == "b"} {Token.string == "."}
	   {Token.string == "c"} {Token.string == "."})
	 
	|
 	  ({Token.string == "B"} {Token.string == "."}
	   {Token.string == "C"} {Token.string == "."})
	)

Macro: YEAR
(        
 {Lookup.majorType == year}|
 TWO_DIGIT | FOUR_DIGIT | 
 {Token.string == "'"}
 (TWO_DIGIT)
)


Macro:	XDAY
(
 ({Token.orth == upperInitial} |
  {Token.orth == allCaps})
 {Token.string == "Day"}
)


Macro: ORDINAL
(
   ({Token.string ==~ "[0-9][0-9]?(th|rd|nd|st)"}
    |
   {Lookup.minorType == ordinal})
   (
    {Token.string == "of"})?
)

Macro: NUM_OR_ORDINAL
  (ORDINAL | DAY_MONTH_NUM)


Macro: COMMA
({Token.string == ","})


Macro:  TIME_ZONE
(({Lookup.minorType == zone})|
 ({Token.string == "("}
  {Lookup.minorType == zone}
  {Token.string == ")"})
)

Macro: TIME_DIFF
(
 ({Token.string == "+"}|{Token.string == "-"})
 (FOUR_DIGIT)
)

Macro: TIME_AMPM
(
 ({Token.string == "am"}|{Token.string == "pm"})
)


//================================ RULES ======================================


Rule:	DateYearDot
Priority: 50
// 01.07.00 alebo 1.7.2012
  
(
 (
 (DAY_MONTH_NUM):DAY DOT (DAY_MONTH_NUM):MONTH DOT (YEAR):YEAR)
 )
:date
-->
 :date.SK_DateTime = {kind = "SK_DateTime" rule = "DateYearDot" DAY = :DAY@string MONTH = :MONTH@string YEAR = :YEAR@string}





Rule:	DateDot
Priority: 70
// 01.07. alebo 1.7. alebo 1.7
  
(
 (DAY_MONTH_NUM):DAY DOT (DAY_MONTH_NUM):MONTH 
 (DOT)?
)
:date
-->
 :date.SK_DateTime = {kind = "SK_DateTime" rule = "DateDot" DAY = :DAY@string MONTH = :MONTH@string}
 
 
 
 
Rule:	WordTimeDot
Priority: 60
// o 17.0 ; okolo 17.20
  
(
({Token.string == "o"} | {Token.string == "okolo"})
(ONE_DIGIT|TWO_DIGIT):HOUR
DOT 
(TWO_DIGIT):MINUTE
)
:date
-->
 :date.SK_DateTime = {kind = "SK_DateTime" rule = "WordTimeDot" HOUR = :HOUR@string MINUTE = :MINUTE@string}
 
 
 
 
 
 Rule: TimeDigital1
// 20:14:25
(
 (ONE_DIGIT|TWO_DIGIT):HOUR
 {Token.string == ":"} 
 (TWO_DIGIT):MINUTE 
({Token.string == ":"}
(TWO_DIGIT):SEC
)? 
)
:time
-->
:time.SK_DateTime = {kind = "SK_DateTime" rule = "TimeDigital1" HOUR = :HOUR@string MINUTE = :MINUTE@string SEC = :SEC@string}





Rule:	TimeDigital2

// 8:14 am
// 4.34 pm
// 6am

(
 (ONE_DIGIT|TWO_DIGIT):HOUR 
 (({Token.string == ":"}|{Token.string == "."} |{Token.string == "-"} )
  (TWO_DIGIT):MINUTE )?
 (TIME_AMPM):AMPM
 (TIME_ZONE)?
)
:time
-->
:time.SK_DateTime = {kind = "SK_DateTime" rule = "TimeDigital" HOUR = :HOUR@string MINUTE = :MINUTE@string AMPM = :AMPM@string}